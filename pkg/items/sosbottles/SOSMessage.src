use uo;
use os;

include ":coords:coords";
include ":itemutils:canAccess";
include ":gumps:include/gumps";

program use_SOSMessage( who, message )

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");

	if( !can_access(who, message) )
		return 0;
	endif
	if( message.movable == 0 )
		SendSysMessage(who, "Voce nao pode usar isto.");
		return 0;
	endif
	if( !(message in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "Este item nao esta na sua mochila.");
		return 0;
	endif

	detach();
        var SOS_Gump := SOS_LoadGump(who, message);
        GFSendGump(who, SOS_Gump);

endprogram


function SOS_LoadGump(who, message)

	var tox := GetObjProperty(message, "tileX");
	var toy := GetObjProperty(message, "tileY");
	var msgindex := GetObjProperty(message, "msgnum");

	if( tox.errortext || toy.errortext)
		syslog("bad message "+tox+" "+toy);
		DestroyItem(message);
		SendSysMessage(who, "A mensagem esta impossivel de ser lida!");
		return 0;
	endif
	var coords := ToSextant(tox, toy);
	var msgLineOne, msgLineTwo, msgLineThree;
	case (msgindex)
		0: 	msgLineOne   := "Ajuda! nosso navio esta afundando! Estamos";
			msgLineTwo   := "nas coordenadas " + coords + "!  Por favor";
			msgLineThree := "venha logo, estamos afundando rapido!";

		1: 	msgLineOne   := "Nos estamos sob ataque de uma gigantesca";
			msgLineTwo   := "serpente marinha! Estamos nas coordenadas";
			msgLineThree :=  + coords + "!  Por favor nos ajude!";

		2: 	msgLineOne   := "Colidimos com recifes e o casco do navio";
			msgLineTwo   := "quebrou. Estamos nas coordenadas " + coords + "!";
			msgLineThree := "Vamos tentar fazer reparos o maximo que der.";

		3: 	msgLineOne   := "Nos encontramos uma terrivel tempestade";
			msgLineTwo   := "nas coordenadas " + coords + "!";
			msgLineThree := "O navio vai naufragar logo, nos ajude!";
	endcase

        var gump := GFCreateGump();
	GFResizePic(gump, 0, 0, 3500, 300, 200);
        GFAddButton(gump, 110, 140, 1148, 1149, GF_CLOSE_BTN, 1);
	GFTextLine(gump, 20, 30, 0, msgLineOne);
	GFTextLine(gump, 20, 50, 0, msgLineTwo);
	GFTextLine(gump, 20, 70, 0, msgLineThree);

	return gump;

endfunction
