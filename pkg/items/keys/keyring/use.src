/*
 * $Id: use.src 1186 2006-02-10 19:42:11Z austin $
 *
 */
 
use uo;
use os;

include ":keys:key";

program UseKeyRing(who, keyring)
	SendSysMessage(who, "What do you want to use that on?");
	
	var targ := Target(who);
	if ( !targ.IsA(POLCLASS_ITEM) )
		SendSysMessage(who, "Cancelled.");
		return 0;
	elseif ( targ == keyring )
		RemoveKeys(who, keyring);
		return 1;
	elseif ( !KP_IsLockable(targ) )
		SendSysMessage(who, "That is not lockable.");
		return 0;
	endif

	if ( KP_HasKeyForLock(keyring, targ) )
		KP_ToggleLock(targ, who);
		return 1;
	else
		SendSysMessage(who, "You do not have a key for that lock.");
		return 0;
	endif
endprogram

function RemoveKeys(who, keyring)

	if ( keyring.container != who.backpack )
		SendSysMessage(who, "That keyring must be in your backpack to remove its keys.");
		return 0;
	endif
	
	var contents := EnumerateItemsInContainer(keyring);
	foreach item in contents
		MoveItemToContainer(item, who.backpack);
		sleepms(2);
	endforeach
	
	return 1;
endfunction