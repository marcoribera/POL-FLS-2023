// $Id: methods.src 87 2006-05-23 19:04:43Z sroyalty $

use uo;
use util;
use os;

program Install()
	return 1;
endprogram

exported function IsCorpse(corpse)
	corpse := corpse;
	return 1;
endfunction

exported function GetOwner(corpse)
	if ( corpse.ownerserial )
		return SystemFindObjectBySerial(corpse.ownerserial);
	elseif ( CInt(GetObjProperty(corpse, "ownerserial")) )
		return SystemFindObjectBySerial(CInt(GetObjProperty(corpse, "ownerserial")));
	endif
endfunction

exported function SetOwner(corpse, serial)
	if ( serial.IsA(POLCLASS_UOBJECT) )
		serial := serial.serial;
	endif
	return SetObjProperty(corpse, "ownerserial", serial);
endfunction

exported function NPCTemplate(corpse)
	var template := GetObjProperty(corpse, "npctemplate");
	if ( template )
		return template;
	else
		return error{"errortext":="Corpse did not belong to an npc."};
	endif
endfunction

exported function IsPlayerCorpse(corpse)
	var owner := GetOwner(corpse);

	if ( owner )
		return owner;
	else
		return error{"errortext":="Corpse does not have an owner."};
	endif
endfunction

exported function SetSkinned(corpse, skinner)
	var serial;

	if ( skinner.ISA(POLCLASS_MOBILE) )
		serial := skinner.serial;
	else
		serial := skinner;
	endif

	var corpse_name := corpse.desc;
	if ( !corpse_name["[skinned]"] )
		// Add "[skinned]" to the corpse description.
		SetName(corpse, corpse_name+" [skinned]");
	endif

	//Store the serial of who skinned the corpse.
	SetObjProperty(corpse, "Skinned", serial);
endfunction

exported function IsSkinned(corpse)
	return CInt(GetObjProperty(corpse, "Skinned"));
endfunction

exported function MakeRemains(corpse)
	if ( !corpse.IsA(POLCLASS_CORPSE) )
		return error{"errortext":="Object is not a corpse."};
	endif

	//Caso n찾o tenha erro para retornar o corpo, ele vai criar a remains
	if (!CInt(GetObjProperty(corpse, "erro_retornar_corpo")))
		var remains := CreateItemAtLocation(corpse.x, corpse.y, corpse.z, "CorpseRemains", 1, corpse.realm);
		if ( remains.errortext )
			return remains;
		endif

		//Adiciona propriedade para permitir mover os itens para um remains
        SetObjProperty(remains, "allow_temp_move", 1);

		foreach item in ( EnumerateItemsInContainer(corpse) )
			if ( item.container != corpse )
				continue;
			endif

            var errorMoveItemToContainer := MoveItemToContainer(item, remains, item.x, item.y);
            if (!errorMoveItemToContainer)
                Syslog("Erro ao mover o item para o remains: " + errorMoveItemToContainer.errortext);
            endif

			item.decayAt := 0;
		endforeach

		var realowner := IsPlayerCorpse(corpse);
		if (realowner)
			remains.SetOwner(realowner);
		endif

		//A prop de realname n찾o existe mais nesse ponto, tem que pegar
		//o name do atributo name do corpse
		var charname := StrReplace(corpse.name, "corpo de ", "");
		SetObjProperty(remains, "charname", charname);
		SetName(remains, "Restos de "+ charname);
		if (GetObjProperty(corpse, "deathdetails"))
			SetObjProperty(remains, "deathdetails", GetObjProperty(corpse, "deathdetails"));
			SetName(remains, "Restos de Corpo");
		endif

        //N찾o permite inserir mais itens nos remains, ap처s mover os itens do body -> remains
        EraseObjProperty(remains, "allow_temp_move");

		DestroyItem(corpse);

		return remains;
	endif
endfunction

exported function Gibbify(corpse, range)
	var piece_list := array
	{
		0x1DAD, //Torso
		0x1DB2, //Left Leg
		0x1DB1, //Right Leg
		0x1DB0, //Left Arm
		0x1DAF, //Right Arm
		0x1CEE, //Liver
		0x1CED, //Heart
		0x1CF0  //Brain
	};

	foreach objtype in (piece_list)
		var item := CreateItemAtLocation(corpse.x, corpse.y, corpse.z, objtype, 1);

		var newx, newy;
		var num_tries := 0;

		MOVE_LOOP:
		repeat
			newx := RandomInt(range)-CInt(range/2);
			newy := RandomInt(range)-CInt(range/2);

			newx := newx+corpse.x;
			newy := newy+corpse.y;

			num_tries := num_tries+1;
			if ( num_tries > 30 )
				num_tries := 0;
				newx := corpse.x;
				newy := corpse.y;
				break MOVE_LOOP;
			endif
		until ( CheckLosAt(corpse, newx, newy, corpse.z) );

		MoveObjectToLocation(item, newx, newy, corpse.z, corpse.realm, MOVEOBJECT_FORCELOCATION);
	endforeach

	DestroyItem(corpse);

	return 1;
endfunction