use uo;
use cfgfile;

include ":dyes:dyescouro";

program UseDyes(mobile, dyescouro)
	if( !ReserveItem(dyescouro) )
		SendSysMessage(mobile, "Those dyes are already in use.");
		return 0;
	elseif( !(dyescouro in EnumerateItemsInContainer(mobile.backpack)) )
		SendSysMessage(mobile, "That item is not in your backpack.");
		return 0;
	endif

	SendSysMessage(mobile, "What do you want to use these dyes on?");
	var targ := target(mobile);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled");
		return 0;
	elseif( !ReserveItem(targ) )
		SendSysMessage(mobile, "That is already in use.");
		return 0;
	endif

	if ( (CanDyeCouro(targ)) || (mobile.cmdlevel) )
		var color := SelectColor(mobile, targ);
		if ( ValidDyeColor(mobile, color) )
			PlaySoundEffect(mobile, 0x25);
			targ.color := color;
		endif
		return 1;
	else
		SendSysMessage(mobile, "You can not dye that.");
		return 0;
	endif
endprogram