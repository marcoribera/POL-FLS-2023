use uo;
use os;
use polsys;

include ":attributes:attributes";
include "include/dungeons";
include ":timedScripts:timedScripts";
include ":tn:cooldown";

program sanctuary(params)
	var who := params[1];

    if (InDarkness(who) && TemHabilidade(who, "Caminhar nas sombras"))
        var duration_valsharess := cint(AP_GetSkill(who, MAGICLORE)/5);
        TS_StartTimer(who, "velocidade", duration_valsharess);
    endif

    if (InDarkness(who))
        var duration_shadowdancer := cint(AP_GetSkill(who, MAGICLORE)/2.5);
        Setcooldown(who, "spectre", duration_shadowdancer);
    endif

    var duration := Cint(AP_GetSkill(who, MAGICLORE) / 8);
    var i := 0;
    PlayObjectCenteredEffectHuefx(who, 0x54CE, 10, 20, 1420, 1);
    PLaySoundEffect(who, 0x204);
	while (protected(who) && i < duration)
        who.concealed := 1;
		sleep(1);
        i++;
	endwhile

    who.concealed := 0;
endprogram

function protected(who)
    if (who.warmode && !TemHabilidade(who, "Forma Umbral"))
        SendSysMessage(who, "Voce entrou em modo de combate, saindo das sombras");
		return 0;
	endif

	return 1;
endfunction

function PlayObjectCenteredEffectHuefx( center, effect, speed, loop := 0, color := 0, translucent := 0 )
    var packetString := "C0";
    packetString := packetString + "03";
    packetString := packetString + fixPacketLength(hex(center.serial),4);
    packetString := packetString + fixPacketLength(hex(0),4);
    packetString := packetString + fixPacketLength(hex(effect),2);
    packetString := packetString + fixPacketLength(hex(0),10);
    packetString := packetString + fixPacketLength(hex(speed),1); //speed
    packetString := packetString + fixPacketLength(hex(loop),1); //loop
    packetString := packetString + "000000000000" ;
    packetString := packetString + fixPacketLength(hex(color),2); //color
    packetString := packetString + "000000";
    packetString := packetString + fixPacketLength(hex(translucent),1); //tran
    foreach chr in ListMobilesNearLocation( center.x, center.y, center.z, 16 );
        SendPacket( chr, packetString );
    endforeach
endfunction

function fixPacketLength( packetString, byteLength )
    if( !packetString || packetString == error )
        packetString := 0;
    endif
    packetString := cStr(packetString);

    if( packetString[1,2] == "0x" )
        packetString := cStr(packetString[3,len(packetString)]);
    endif

    if( len(packetString) > byteLength * 2 )
//		var extraBytes := (byteLength*2) - len(packetString);
		var extraBytes := len(packetString) - (byteLength*2) + 1;
		return cStr(packetString[extraBytes,len(packetString)]);
    endif

    while( len(packetString) < byteLength * 2 )
        packetString := "0" + packetString;
    endwhile

    return packetString;
endfunction
