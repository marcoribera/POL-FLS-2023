use cfgfile;
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":tn:tngumps";
include ":gumps:requestGump";
include ":attributes:attributes";
include "include/client";
include ":disguise:disguise";
include ":charactercreation:createchar";
include ":datafile:datafile";
include "include/say";
include "include/creationUtils";

program epicCreationGump(who)
    var culture_pic := culturalSelection(who);
    racialSelection(who, culture_pic);
    classSelection(who, culture_pic);
endprogram

function culturalSelection(who)
    var chardata := struct;
    var culture_pic := 0x9E7;
    var btn_value;
    while (1)
        var input := CultureGump(who, culture_pic);
        btn_value := input[0]; 

        if (btn_value == 201)
            culture_pic++;
        elseif (btn_value == 210) 
            culture_pic--;
        else
            break;
        endif

        if (culture_pic < 0x9E7)
            culture_pic := 0x9EB;
        elseif (culture_pic > 0x9EB)
            culture_pic := 0x9E7;
        endif
    endwhile
    chardata.+culture := getCultureByPictureId(culture_pic);
    SetObjProperty(who, "chardata", chardata);

    SendSysMessageEX(who, "Sua cultura será "+chardata.culture, SSM_INFO);
    return culture_pic;
endfunction

function racialSelection(who, culture_pic)
    var btn_value;
    var chardata := GetObjProperty(who, "chardata");
    while (1)
        var input := RaceGump(who, culture_pic);
        foreach key in (input.keys)
            btn_value := key;
        endforeach
        if (btn_value == 201)
            chardata.race := "Anao";
            break;
        elseif (btn_value == 202) 
            chardata.race := "Drow";
            break;
        elseif (btn_value == 203) 
            chardata.race := "Elfo";
            break;
        elseif (btn_value == 204) 
            chardata.race := "Humano";
            break;
        elseif (btn_value == 205) 
            chardata.race := "Orc";
            break;
        elseif (btn_value == 206) 
            chardata.race := "Polski";
            break;
        elseif (chardata.culture == CULTURA.AIGLANA || chardata.culture == CULTURA.LESTE)
            SendSysMessageEX(who, "Escolha sua raça!", SSM_FAIL);
        else
            chardata.race := getRaceByCulturalId(culture_pic)[1];
            break;
        endif
    endwhile
    SendSysMessageEX(who, "Voce será um "+chardata.race, SSM_INFO);

    SetObjProperty(who, "chardata", chardata);
endfunction

function classSelection(who, culture_pic)
    var btn_value;
    var chardata := GetObjProperty(who, "chardata");
    while (1)
        var input := ClassGump(who, culture_pic);
        foreach key in (input.keys)
            btn_value := key;
        endforeach
        if (btn_value == 201)
            chardata.+classe := "Guerreiro";
            break;
        elseif (btn_value == 202) 
            chardata.+classe := "Ladino";
            break;
        elseif (btn_value == 203) 
            chardata.+classe := "Sabio";
            break;
        elseif (btn_value == 204) 
            chardata.+classe := "Artifice";
            break;
        else
            SendSysMessageEX(who, "Escolha seu caminho", SSM_FAIL);
        endif
    endwhile

    SetObjProperty(who, "chardata", chardata);
    SendSysMessageEX(who, "Você escolheu o caminho do "+chardata.classe, SSM_INFO);
endfunction
