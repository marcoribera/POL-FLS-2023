use uo;
use os;
use cfgfile;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";
include ":attributes:attributes";
include ":tn:npcutil";
include ":brainAI:npcUtil";
include ":ghaia:ghaia";
include ":spells:summon";
include ":taming:taming";
include ":charactercreation:habilidades";

program summontotem(params)

	var who := params[1];
	var targ := params[2];
	var spellid := params[3];
	params := 0; // No longer needed

	//Efeito da magia
	PlaySoundEffect(targ, SFX_SPELL_HEAL);

	var cfg  :=  ReadConfigFile(":spells:allspells");
	var elem := FindConfigElem(cfg, spellid);
	var template := getConfigString(elem, "npcTemplate");
	var cfg2 := ReadConfigFile(":brainAI:npcdesc");
	var npctemplate := template;
	npctemplate := npctemplate[10, len(npctemplate)-9];
	cfg2 := cfg2[npctemplate];
	var petcost := 0;
	if (!Cint(cfg2.petslot))
		petcost := 1;
	else
		petcost := Cint(cfg2.petslot);
	endif

	//sendsysmessage(who, " " + GetNumPets(who) );
	if (GetMaxSummons(who) < GetNumSummons(who) + petcost )
		SendSysMessageEx(who, "Voce tem " + GetNumSummons(who) + " invocacoes sob seu comando e o maximo sao : " + GetMaxSummons(who)  + "  . "  , SSM_FAIL);
		return;
	endif

	if (template == error)
		SendSysMessage(who, "erro ao ler o template" + template);
		return;
	endif

	var espirito := summon(template, targ, who);

	if (espirito)
		SendSysMessageEx(who, "Voce teve sucesso em invocar a criatura.", SSM_INFO);
		AddPet(who, espirito.serial);
	else
		SendSysMessageEx(who, "Voce falhou em invocar a criatura.", SSM_FAIL);
		return;
	endif

	var fator := (AP_GetSkill(who, MAGICLORE)-20.0)/4; //skill 0 -> -5 // Skill 20 -> 0 // Skill 40 -> +5 // Skill 100 -> +20

	AP_SetTrueSkill(espirito, WRESTLING, AP_GetTrueSkill(espirito, WRESTLING) + fator);
	AP_SetTrueSkill(espirito, TACTICS, AP_GetTrueSkill(espirito, WRESTLING) + fator);

	if (temHabilidade(who, "Espirito Protetor"))
		espirito.resist_physical_mod  := 25;
		espirito.resist_cold_mod  := 25;
		espirito.resist_poison_mod  := 25;
		espirito.resist_energy_mod  := 25;
		espirito.resist_fire_mod  := 25;
		SetObjProperty(espirito, "#ReflexosMod", 25);
		SetObjProperty(espirito, "#ReflexosMod", 25);
		SetObjProperty(espirito, "#VontadeMod", 25);
	endif

	var duracao := 25 + (60*CInt(AP_GetSkill(who, MAGICLORE))/100); //skill 0 -> 25s // Skill 20 -> 37s // Skill 40 -> 49s // Skill 100 -> 85s

	if (temHabilidade(who, "Invocacao Duradoura"))
		duracao := duracao*2; //skill 0 -> 50s // Skill 20 -> 72s // Skill 40 -> 98s // Skill 100 -> 170s
	endif

	TS_StartTimer(espirito, "summon", duracao );

	return 1;

endprogram

