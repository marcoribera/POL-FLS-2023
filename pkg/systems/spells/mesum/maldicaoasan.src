    use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		SendSysMessageEx(targ, "Voce resistiu a magia.", SSM_INFO);
		return;
	endif


	PlaySoundEffect(targ, SFX_SPELL_WEAKEN);

	var time := 0;
	var oldx := who.x;
	var oldy := who.y;

	who.frozen := 1;

	Corromper(who, 13, targ);

	if(TemHabilidade(who, "Vontade Esmagadora"))
		if (CheckResist(targ, VONTADE, 70, -CInt(AP_GetSkill(who, MAGICLORE)/2.5))) // Muito mais Dif√≠cil de resistir
			SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
			who.frozen := 0;
		return;
		endif
	else
		if (CheckResist(targ, VONTADE, 30, -CInt(AP_GetSkill(who, MAGICLORE)/2.5)))
			SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
			who.frozen := 0;
		return;
		endif
	endif

	targ.frozen := 1;

	while  (time < 10)

		if ( (who.x != oldx) || (who.y != oldy))
			SendSysMessageEx(who, "Voce se moveu e cancelou a magia.", SSM_FAIL);
			break;
		endif

		if (Distance(who, targ) >= 12)
			SendSysMessageEx(who, "O alvo se distanciou muito.", SSM_FAIL);
			break;
		endif

		var mydmg := cint(AP_GetVital(who, HITS)/10);
		var targdmg := cint(AP_GetVital(targ, HITS)/10);
//		ApplyRawDamage( targ, targdmg, 0, DAMAGE_SHOW );
//		ApplyRawDamage( who, mydmg, 0, DAMAGE_SHOW );

		ApplyRawDamage( targ, targdmg );
		ApplyRawDamage( who, mydmg);

		if(targ.dead)
			break;
		endif

		PerformAction(who, ANIM_CAST_AREA);
		PlayObjectCenteredEffect(who, GFX_RED_SPARKLES, 7, 7);
		PlayObjectCenteredEffect(targ, GFX_RED_SPARKLES, 7, 7);
		time := time + 1;
		sleep(1);
	endwhile

	who.frozen := 0;
	targ.frozen := 0;

	return 1;
endprogram
