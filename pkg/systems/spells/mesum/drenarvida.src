use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif
	SetObjProperty(who, "#Casting",1);

	PlaySoundEffect(targ, SFX_SPELL_WEAKEN);

	var time := 0;
	var oldx := who.x;
	var oldy := who.y;

	//who.frozen := 1;

	var amt := 10;
	if (temHabilidade(who, "Maldicao do Sangue de Asan"))
		if (isBloodied(who))
			amt := CInt(amt*1.5);
		endif
	endif

	Corromper(who, 9, targ);

	if(TemHabilidade(who, "Vontade Esmagadora"))
		if (CheckResist(targ, VONTADE, 70, -CInt(AP_GetSkill(who, MAGICLORE)/2.5))) // Muito mais Dif√≠cil de resistir
			SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
			SendSysMessageEx(targ, "Voce resistiu a magia.", SSM_INFO);
			//who.frozen := 0;
			EraseObjProperty(who, "#Casting");
			return;
		endif
	else
		if (CheckResist(targ, VONTADE, 30, -CInt(AP_GetSkill(who, MAGICLORE)/2.5)))
			SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
			SendSysMessageEx(targ, "Voce resistiu a magia.", SSM_INFO);
			//who.frozen := 0;
			EraseObjProperty(who, "#Casting");
			return;
		endif
	endif

	while  (time < 10)
		if ( (who.x != oldx) || (who.y != oldy))
			time:=10;
			SendSysMessageEx(who, "Voce se moveu e cancelou a magia.", SSM_FAIL);
		elseif (Distance(who, targ) >= 16)
			time:=10;
			SendSysMessageEx(who, "O alvo se distanciou muito.", SSM_FAIL);
		elseif(AP_GetVital(targ, HITS)<=0)
			time:=10;
		else
			PlayMovingEffectEx(targ, who, 3982, 255, 1, 2117);
			PerformAction(who, ANIM_CAST_AREA);
			ApplyRawDamage( targ, amt );
			PrintTextAbovePrivate(targ, "-"+amt + " Vida", who, 3, 38); // vermelho
			HealDamage( who, amt);
			PrintTextAbovePrivate(who, "+"+amt + " Vida", who, 3, 568); // verde
			time := time + 1;
			sleep(1);
		endif
	endwhile
	//who.frozen := 0;
	EraseObjProperty(who, "#Casting");
	return 1;
endprogram
