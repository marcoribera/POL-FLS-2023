use uo;
use os;
use cfgfile;

include "include/say";
include ":spells:spellbooks";
include ":charactercreation:habilidades";

program magia(who, spellname)
	var spellinfo := struct;

	if ( GetObjProperty(who, "#Casting") )
		SendSysMessageEx(who, "Voce nao pode invocar um encanto agora.", SSM_FAIL);
		return 0;
	endif

	var cfg;
	if(GetObjProperty(who, "chardata").magia == "kitahster" || GetObjProperty(who, "advclass") == "kitahster")
        cfg := ReadConfigFile(":spells:config/sangriaspells");
		spellinfo.+magic_path := "Sangria";
	else
		cfg := ReadConfigFile(":spells:allspells");
    endif

	var elem;
	var id := 1;
	var achou := 0;
	foreach spell in ( GetConfigIntKeys(cfg) )
		elem := FindConfigElem(cfg, id);
		if ( GetConfigString(elem, "Name") == spellname)
			achou := 1;
			break;
		endif
		id := id + 1;
	endforeach

	if (!achou)
		SendSysMessageEx(who, "A magia " + spellname + " nao existe.", SSM_FAIL);
		return;
	endif

	var spellbook := 0;
	var booktype;
	var donoGrimorio;
	foreach item in ( EnumerateItemsInContainer(who.backpack) )
		if ( item.objtype == 24832 || item.objtype == 24835) //livros livro das maldicoes ou livro da sangria
			donoGrimorio := GetObjProperty(item, "serialDono");
			if (donoGrimorio == who.serial)
				var spells := GetObjProperty(item, "spells");
				foreach s in spells
					if ( s.id == id)
						spellbook := 1;
						booktype := GetBookType(item);
						break;
					endif
				endforeach
			endif
		endif
	endforeach

	var circle_spell := cint(GetConfigString(elem, "Circle"));
	if ( !spellbook && !(GetObjProperty(who, "advclass") == "kitahster" && circle_spell == 0))
		SendSysMessageEx(who, "Voce nao possui a magia " + spellname + " nos seu grimorio.", SSM_FAIL);
		return;
	endif

	spellinfo.+id := id;
	var script := Start_Script(":spells:cast_spell", {who, spellinfo, booktype});
	if ( script.errortext )
		SendSysMessage(who, "Error - Could not start spell starter ->"+script.errortext);
		return 0;
	endif
endprogram
