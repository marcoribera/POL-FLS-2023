use uo;
include "include/tileEffects";
include ":attributes:attributes";
include ":fls_magery:spells";
include "include/damage";
include "include/shapes";
include ":timedScripts:timedScripts";
include "include/sysEvent";
include "include/client";
include "include/say"; 
include ":taming:taming";

program spell_fireball(params)
    var caster := params[1];
    var info := params[2];
	params := 0; // No longer needed
    var cast_on;

    cast_on := MS_Target(caster, info.targ, "Selecione um alvo.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := params[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "cancelado.", color := 33);
        return 0;
    endif

    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

    var coord_list := GetCoordsInLine(caster.x, caster.y, cast_on.x, cast_on.y);
	foreach coord in coord_list
		if ( _coord_iter < 4 ) // Dont make one where its standing
			continue;
		endif
		
		var field := CreateItemAtLocation(coord.x, coord.y, caster.z, "FireField_NS", 1);
		field.SetDuration(0);
		sleepms(105);
	endforeach

	foreach coord in GetCoordsInSquare(cast_on.x, cast_on.y, 5)
		if ( RandomInt(2) != 1 ) // Don't place a fire field
			continue;
		endif
		case( RandomInt(3) )
			default:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_1, 10, 10, 0);
				break;
			1:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_2, 10, 10, 0);
				break;
			2:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_3, 10, 10, 0);
				break;
		endcase
		var field;
		if ( RandomInt(2) )
			field := CreateItemAtLocation(coord.x, coord.y, cast_on.z, "FireFieldNS", 1);
		else
			field := CreateItemAtLocation(coord.x, coord.y, cast_on.z, "FireFieldEW", 1);
		endif
		field.SetDuration(20);
		sleepms(5);
	endforeach

    PlayMovingEffect(caster, cast_on, GFX_LARGE_FIREBALL, 10, 10, 1);
    PlaySoundEffect(cast_on, 0x208);
    
    var alreadydamaged := array{};
    var dmg := cint(AP_GetSkill(caster, MAGICLORE))/5 + RandomDiceRoll("4d10");
    var mobiles := ListMobilesNearLocation(caster.x, caster.y, caster.z, 1, caster.realm);
    var pets := ListPetsNear(caster, 8);
    foreach mobile in mobiles
        if (!(mobile.serial in alreadydamaged) && (mobile != caster) && !(mobile in caster.party.members) && !(mobile in pets ))
            alreadydamaged.Append(mobile.serial);
            DamageFLS(mobile, cint(dmg), DMG_FIRE, caster);
        endif
        sleepms(20);
    endforeach
endprogram

