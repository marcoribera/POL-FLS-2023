////////////////////////////////////////////////////
//
//   MultiImport - ReImport Multi for Modification
//
//     Author: Shinigami
//     eMail : Shinigami@gmx.net
//
//   Modifications:
//
////////////////////////////////////////////////////
//
// Needs:
//   statics/multi.cfg
//
////////////////////////

/////////////////
// Bibliotheken
/////////////////

Use uo;
Use os;
Use cfgfile;

/////////////
// Includes
/////////////

Include ":worldforge:errormsg_wf";

///////////////
// Konstanten
///////////////

Const ERRORMSGS_GERMAN:=
  "{}MultiImport: ImportArea aktiviert..."+
  "{}MultiImport: ImportArea beendet."+
  "{}MultiImport: Item %% an %% %% %% konnte nicht erzeugt werden!"+
  "{}Die Items wurden importiert."+
  "{}Probleme beim Lesen der Configdateien!";
Const ERRORMSGS_ENGLISH:=
  "{}MultiImport: ImportArea started..."+
  "{}MultiImport: ImportArea done."+
  "{}MultiImport: Was impossible to create Item %% at %% %% %%!"+
  "{}Items have been imported."+
  "{}Problems on Reading Config File!";

/////////////////
// Hauptprogram
/////////////////

Program TextCMD_MultiImport(who)
  Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

  PrintErrorMessage(1);
  ImportArea(who);
  PrintErrorMessage(2);
EndProgram

//////////////////////////////////////
// ImportArea - Importiert die Items
//////////////////////////////////////

Function ImportArea(who)
  Var datafile:=ReadConfigFile(":worldforge:config/multi");
  If (datafile)
    Var min_x:=6144;
    Var min_y:=4096;
    Var object;
    Var item;

    ForEach name in GetConfigStringKeys(datafile)
      item:=datafile[name];
      If (item)
        If (min_x>item.x)
          min_x:=item.x;
        EndIf
        If (min_y>item.y)
          min_y:=item.y;
        EndIf
      EndIf
    EndForEach

    min_x:=min_x-who.x;
    min_y:=min_y-who.y;

    ForEach name in GetConfigStringKeys(datafile)
      item:=datafile[name];
      If (item)
        object:=CreateItemAtLocation(item.x-min_x, item.y-min_y, item.z, item.id, 1);
        If (object)
          If (item.name)
            SetName(object, item.name);
          EndIf
          object.color:=item.color;
          object.decayat:=0;
          object.movable:=0;
        Else
          PrintErrorMessage(3, {item.id, item.x, item.y, item.z});
        EndIf
      EndIf
    EndForEach

    UnloadConfigFile(":worldforge:config/multi");

    PrintErrorMessage(4, { }, who);
  Else
    PrintErrorMessage(5, { }, who);
  EndIf
EndFunction