use uo;
use os;
use util;

include "include/client";
include ":attributes:attributes";
include ":attributes:skillCheck";

CONST ITEM_SWARM := 0x091b;
CONST SOUND_SCRUNCH := 0x58;
CONST ACTION_BOW := 0x0020;
CONST UOBJ_HONEY := 0x9ec;
CONST UOBJ_WAX := 0x1422;
CONST COLMEIA_FONT_COLOR_NEUTRAL := 0;
CONST COLMEIA_FONT_COLOR_BAD := 40;
CONST COLMEIA_FONT_COLOR_GOOD := 90;

CONST MAX_PER_HIVE	:= 10;

program harvest_honey (character, beehive)
	//Verifica a distância entre o personagem e a Colmeia
	if (Distance(character, beehive) > 1)
		SendSysMessage(character, "Você está muito distante!", FONT_NORMAL, COLMEIA_FONT_COLOR_BAD);
		return 0;
	else
		SendSysMessage(character, "Você começou a mexer na Colmeia.", FONT_NORMAL, COLMEIA_FONT_COLOR_NEUTRAL);
		var already_harvested_amount;
		//armazena a posição inicial do personagem
		var character_x1 := character.x;
		var character_y1 := character.y;
		repeat
			PlaySoundEffect(character, SOUND_SCRUNCH);
			PerformAction(character, ACTION_BOW);
			sleep (2);
			PlaySoundEffect(character, SOUND_SCRUNCH);
			PerformAction(character, ACTION_BOW);
			sleep (3);
			if ((character.x == character_x1) && (character.y == character_y1))
				//Verifica se já acabou a quantidade de cera e mel disponíveis na Colmeia
				already_harvested_amount := CInt(GetObjProperty (beehive, "#harvested"));
				if (already_harvested_amount >= MAX_PER_HIVE)
					SendSysmessage (character, "Não há mais nada para ser retirado daqui.", FONT_NORMAL, COLMEIA_FONT_COLOR_BAD);
					
					return 1;
				//Testa a skill para retirar a cera de abelha e o mel
				elseif (SkillCheck(character, DomesticWork, 20, 0) > 0)
					if (!already_harvested_amount)
						SetObjProperty(beehive, "#harvested", 1);
					else
						SetObjProperty(beehive, "#harvested", already_harvested_amount + 1);
					endif
					//Coloca os recursos coletados na bolsa
					CreateItemInBackpack(character, UOBJ_HONEY, 1);
					CreateItemInBackpack(character, UOBJ_WAX, 1);
					SendSysmessage (character, "Coletou um pouco de mel e cera de abelha.", FONT_NORMAL, COLMEIA_FONT_COLOR_GOOD);
				else
					SendSysmessage (character, "Falhou em coletar mel e cera de abelha.", FONT_NORMAL, COLMEIA_FONT_COLOR_NEUTRAL);
				endif
				//Testa se as abelhas ficaram furiosas
				if (!(SkillCheck(character, HANDLEANIMAL, 15, 0) > 0))
					if (RandomInt (4) == 1)
						CreateItemAtLocation(beehive.x, beehive.y, beehive.z, ITEM_SWARM, 1, character.realm);
						SendSysmessage(character, "Oh não...Abelhas!!!", FONT_NORMAL, COLMEIA_FONT_COLOR_BAD);
						return 0;
					endif
				endif
			else
				SendSysMessage(character, "Você andou e parou de mexer na Colmeia.", FONT_NORMAL, COLMEIA_FONT_COLOR_BAD);
				return 0;
			endif
		until ((character.x != character_x1) || (character.y != character_y1));
	endif
endprogram
