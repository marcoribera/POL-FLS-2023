// //$Id: makeBlacksmithItems.src 984 2007-01-21 18:14:11Z sroyalty $

use cfgfile;
use util;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";
include ":crafting:crafting";
include ":crafting:craftingRepair";
include ":crafting:toolWear";

var item_config     := ReadConfigFile(":*:itemdesc");
var craft_config    := ReadConfigFile(":stonework:stonework");

program use_SmithTool(who, tool)

	if ( !ReserveItem(tool) )
		SendSysMessage(who, "That is already in use.");
		return 0;
	elseif( (!tool.movable) || !ReserveItem(tool) )
		SendSysMessage(who, "You cannot use that");
		return 0;
	elseif( !tool in ListEquippedItems(who) )
		if( !(tool in EnumerateItemsInContainer(who.backpack)) )
			SendSysMessage(who, "That item is not in your backpack nor equipped.");
			return 0;
		endif
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#IsWaiting");

	var rinfo := sendCraftGump(who, "Masonry");

	// OK was pressed, close it on out
	if(rinfo == 0)
		return 0;
	endif

	var the_objtype := rinfo[1];
	var amt         := rinfo[2];
	var repairit    := rinfo[3];
	var resmeltit   := rinfo[4];

	if( repairit )
		repair_this(who, craft_skill);
		return 0;
	elseif( resmeltit )
		resmelt(who, craft_skill);
		return 0;
	endif

	makeBlacksmithItems(who, the_objtype, amt, tool);

endprogram

function makeBlacksmithItems(who, what, amt, tool)

	if( !what )
		SendSysMessage(who, "Cancelled");
		return 0;
	endif

	// Metal is sent for the ResourceList entry type.
//	var itemchk;
//	var use_on;
//	var ptmod;

	var custo := CInt(craft_config[what].custo);
	var items := CraftItem(who, "Masonry", tool, what, amt, 0, 0);
	foreach item in items
		if (item == OUT_OF_MATERIALS) //retornou erro ao inves do item
			return 1;
		endif
		var pedreiro := CInt(craft_config[what].pedreiro);
		if (item)
			if (pedreiro)
				MarkItem(who, item);
				if (custo > 0)
					var plano := FindObjtypeInContainer(who.backpack, 0x5004);
					if (!plano)
						SendSysMessageEx(who, "Voce nao possui nenhum planejamento para essa construcao.", SSM_FAIL);
						DestroyItem(item);
						return 0;
					endif
					var qtdnovo := GetObjProperty(plano,"pts") - custo;
					if (qtdnovo < 0)
						SendSysMessageEx(who, "Esse planejamento ja nao lhe serve mais.", SSM_FAIL);
						DestroyItem(item);
						DestroyItem(plano);
						return 0;
					else
						SetObjProperty(plano,"pts",qtdnovo);
					endif
					SetName(plano, "Planos de Construcao ["+((500-qtdnovo)/5)+"%%%]");
				endif
			else
				item.movable := 1;
			endif
		endif
	endforeach
	
	return 0;
endfunction

function MarkItem(who, theitem)
       
       theitem.movable := 1;
       MoveObjectToLocation(theitem, who.x, who.y, who.z, who.realm, MOVEOBJECT_FORCELOCATION);
       theitem.movable := 0;
       SetObjProperty(theitem, "pedreiro", who.serial);
       SetObjProperty(theitem, "criador", who.acctname);
       SetObjProperty(theitem, "validade", CInt(ReadGameClock()+180));

endfunction
