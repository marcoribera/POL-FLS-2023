
use cfgfile;
use util;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include "include/client";
include ":timedScripts:timedScripts";
include "include/say";
include ":charactercreation:habilidades";
include ":attributes:skillCheck";
include ":crafting:crafting";
include ":crafting:craftingRepair";
include ":crafting:toolWear";
include ":itemutils:canAccess";
include ":fls_core:fls_crafting";
include ":recipes:recipes";

var item_config     := ReadConfigFile(":*:itemdesc");
var craft_config    := ReadConfigFile(":alchemy:AlchItems");

const REAGENT_START := 0x0f78;
const REAGENT_STOP := 0x0f91;
const SOUND_EFFECT_GRIND := 0x243;
const SOUND_EFFECT_POUR := 0x241;
const SOUND_EFFECT_BUBBLE :=  0x4f;
const UOBJECT_EMPTY_BOTTLE := 0x0f0e;

program use_Alchemy(who, tool)
	if (!Accessible (who, tool) )
		SendSysMessage (who, "Voce nao alcanca aquilo!");
		return;
	endif

	if (!ReserveItem (tool) )
		SendSysMessage (who, "voce nao pode usar isso agora.");
		return;
	endif

	var rinfo := sendCraftGump(who, "Alchemy");

	// OK was pressed, close it on out
	if(rinfo == 0)
		return 0;
	endif

	var the_objtype := rinfo[1];
	var amt         := rinfo[2];

	makeAlchemyItems(who, the_objtype, amt, tool);

endprogram

function makeAlchemyItems(who, what, amt, tool )

	if(!what)
		SendSysMessageEx(who, "Cancelado.", SSM_FAIL);
		return 0;
	endif

	var loop := 0;

	var item := CraftItem(who, "Alchemy", tool, what, amt, 0, 0);

	if(!item)
		while(loop < 3)
			PlaySoundEffect(who, SOUND_EFFECT_GRIND);
			PrintText(who, "*preparando um extrato*");
			sleep(1);
			loop := loop + 1;
		endwhile

		if (RandomInt(100) <= 25)
			FalhaCritica(who, item);
		else
			PlaySoundEffect(who, SOUND_EFFECT_GRIND);
			PrintText(who, "*preparando um extrato*");
			sleep(2);
			PlaySoundEffect(who, SOUND_EFFECT_POUR);
			SendSysMessageEx(who, "Voce nao conseguiu fazer nada com isso.", SSM_FAIL);
			return 0;
		endif
	endif

	return 1;

endfunction


function FalhaCritica(who, item)
	if (TemHabilidade(who, "Alquimista Precavido"))
		return 1;
	endif

	var falha := RandomInt(100)+1;
	var loop := 0;
	if (falha < 65 )
		while(loop < 3)
			PlaySoundEffect(who, SOUND_EFFECT_GRIND);
			PrintText(who, "*preparando um extrato*");
			sleep(1);
			loop := loop + 1;
		endwhile
		PlaySoundEffect(who, SOUND_EFFECT_POUR);
		SendSysMessageEx(who, "Voce nao conseguiu fazer nada com isso.", SSM_FAIL);
		return;
	//elseif ( falha < 68 )
	//	SendSysMEssageEx(who,"AHH!  Seu cabelo caiu!!!", SSM_FAIL);
	//	DestroyItem(GetEquipmentByLayer (who, LAYER_HAIR));
	//	DestroyItem(GetEquipmentByLayer (who, LAYER_BEARD));
	//	return;
	elseif ( falha < 78 )
		SendSysMessageEx(who,"Ugh... Voce nao esta se sentindo muito bem...", SSM_FAIL);
		TS_StartTimer(who, "strPoison", 120, 2);
	elseif ( falha < 82 )
		SendSysMessageEx(who,"Ahh!", SSM_FAIL);
		who.color := 18;
		Detach();
		sleep(180);
		who.color := who.truecolor;
	elseif ( falha < 86 )
		SendSysMEssageEx(who,"O que foi isso?!", SSM_FAIL);
		if (who.graphic == 401)
			who.graphic := 400;
		else
			who.graphic := 401;
		endif
		sleep(600);
		Detach();
		who.graphic := who.objtype;
	elseif ( falha < 95 )
		SendSysMEssageEx(who,"VAI EXPLO...", SSM_FAIL);
		PlayStationaryEffect( who.x, who.y, who.z, 0x36b0, 7, 10, 1);
		PlaySoundEffect(who, 0x208);
		ApplyRawDamage(who, cint(30+ randomInt(100) ) );
	else
		SendSysMessageEX(who,"Voce esta envenenado!!", SSM_FAIL);
		TS_StartTimer(who, "defaultPoison", 120, 5);
	endif
endfunction