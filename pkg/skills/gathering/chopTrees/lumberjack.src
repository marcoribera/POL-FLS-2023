
use uo;
use os;
use math;

include ":attributes:attributes";
include ":datafile:datafile";
include ":gathering:settings";
include ":gathering:lumberJack";
include "include/client";
include ":fls_lumberjacking:fls_lumberjack";
include ":fls_core:fls_objtypes";
include ":crafting:toolWear";
include "include/facings";
include "include/say";

program skill_LumberJack(who,tool)

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");
	SendSysMessage(who, "Escolha a arvore para cortar.");
	var targ := TargetCoordinates(who);
	
	if ( !targ )
		SendSysMessage(who, "Cancelado.");
		return 0;
	endif

	if ( CoordinateDistance(who.x, who.y, targ.x, targ.y) > 1 )
		SendSysMessage(who, "Muito longe.");
		return 0;
	endif

	if ((targ.item.objtype == 0xec24) || (targ.item.objtype == 0xec25) || (targ.item.objtype == 0xec26) || (targ.item.objtype == 0xFF04)||(targ.item.objtype == 0xec27) || (targ.item.objtype == 0xec28))  
		if ((!CheckLineOfSight(who, targ.item)) or (Distance(who, targ.item) > 1))
			SendSysMessage(who, "O alvo esta distante.");
			return;
		else
			var parms;
			var script := start_script (":tn:destroyitems", {who, targ.item, tool});
			return 1;
		endif
	endif 

	if ( !LJ_IsTree(targ.objtype) )
		SendSysMessage(who, "Isto nao e uma arvore.");
		return 0;
	endif



	var orig_x := who.x;
	var orig_y := who.y;

	//SendSysMessage(who," e" + CanChop(who, tool, targ,  orig_x, orig_y));
	while(CanChop(who, tool, targ,  orig_x, orig_y) )
		if ( AP_GetVital(who, "Stamina") <= 2)
			SendSysMessageEx(who, "Voce esta muito cansado para continuar.", SSM_FAIL);
			break;
		endif
		ChopTree(who, tool, targ, orig_x, orig_y);
		AP_ConsumeVital(who, "Stamina", 3);
		CheckToolWear(who, tool, GATHERING );
		sleep(1);
		if (LJ_GetWoodAmount(who.realm, targ.x, targ.y)== 0 )
			PrinttextAbove(who, "*derruba a arvore*");
			break;
		endif
	endwhile

	SendSysMessage(who, "Voce parou de cortar a arvore.");
endprogram

function ChopTree(who, tool, targ, orig_x, orig_y)
	var i:=1;//RandomDiceRoll("3d3");
	for ( i; i>=0; i:=i-1 )
		if ( !CanChop(who, tool, targ, orig_x, orig_y) )
			return 0;
		endif

		PerformAction(who, ANIM_ATTACK_2HAND_DOWN);
		PlaySoundEffect(who, FLS_SFX_SKILL_LUMBERJACK);
		sleepms(1200);
	endfor

	var wood_type := GetWoodType(targ);
	var wood_amount := LJ_GetWoodAmount(who.realm, targ.x, targ.y);

	var max_wood := CInt(LJ_GetSettingsCfgElem("Settings").MaxWood);
	// The less wood there is, the more difficult it becomes.

//	var difficulty := CInt(105 - (100 * wood_amount / max_wood));
	var difficulty := CInt(0 + (max_wood - wood_amount)*3);

	if (difficulty > 30)
		difficulty := 30;
	endif

	if (tool.objtype == 0x0f43)
		difficulty := difficulty - 10;
			if (difficulty < 0)
				difficulty := 0;
			endif
	endif

	//if ( who.cmdlevel )
	//	SendSysMessage(who, "MaxWood="+max_wood+"    CurWood="+wood_amount);
	//	SendSysMessage(who, "Difficulty ->"+difficulty);
	//endif

	if ( SkillCheck(who, GATHERING, difficulty) > 0 )
		var amt := Cint(AP_GetSkill(who, GATHERING)/20);
		if (amt <= 0)
			amt := 1;
		endif
		if (amt > wood_amount)
			amt := wood_amount;
		endif
		wood_amount := wood_amount - amt;
		LJ_SetWoodAmount(who.realm, targ.x, targ.y, wood_amount);
		if (wood_amount < 0)
			wood_amount := 0;
		endif
		var item := CreateItemInBackPack(who, wood_type, amt);
		if ( item.errortext )
			SendSysMessage(who, "Nao foi possivel criar: "+wood_type+" ->"+item.errortext);
			return 0;
		else
			SendSysMessage(who, "Voce colocou a madeira na sua mochila.");
			return 1;
		endif
	endif

	SendSysMessage(who, "Voce e incapaz de conseguir madeira usavel dessa arvore.");
	return 0;
endfunction
function CanChop(who, axe, targ, orig_x, orig_y)
	if ( who.hidden )
		SendSysMessage(who, "Voce parou de cortar a arvore para ficar escondido.");
		return 0;
	elseif ( who.x != orig_x || who.y != orig_y )
		SendSysMessage(who, "Voce parou de cortar a arvore.");
		return 0;
        elseif ( !IsFacing(who, targ.x, targ.y)  )
               SendSysMessage(who, "Voce precisa estar virado para a arvore.");
               return 0;
	elseif ( !(axe in ListEquippedItems(who)) )
		SendSysMessage(who, "Voce parou de cortar a arvore.");
		return 0;
	elseif ( LJ_GetWoodAmount(who.realm, targ.x, targ.y) <= 0 )
		SendSysMessage(who, "Esta arvore ja esta derrubada.");
		return 0;
	elseif ( GetEquipmentByLayer(who, LAYER_MOUNT) )
		SendSysMessage(who, "Voce nao pode cortar arvore montado.");
		return 0;
	elseif ( Distance(who, targ) > 1 )
		SendSysMessage(who, "Voce nao pode cortar de tao longe.");
		return 0;
	endif
	return 1;
endfunction
