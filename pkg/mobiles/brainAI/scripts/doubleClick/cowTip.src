/* $Id: cowTip.src 1155 2006-02-09 08:31:16Z austin $
 *
 */
///////////////////////////////////////////////
// cowTip.src
//
// Created 7/26/2002 11:31AM
// Updated 10/31/2005 11:25AM
//
// Creator: Austin Heilman
// Last Update: MuadDib
//
///////////////////////////////////////////////
//
// This simply makes a double clicked cow tip over
// when double clicked. It goes "mooo" in a whiny
// sort of way too. Players from Wisconsin will
// love it. Never tip a Californian cow though!
// *ahem*
//
///////////////////////////////////////////////
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";

program npcTip(params)
	var npc		:= params[1];
	//var nerve_name:= params[2];
	var event	:= params[3];
	//var settings	:= params[4];
	//var scripts	:= params[5];
	params := 0;

	while ( npc )
		if ( event )
			case ( event.type )
				SYSEVENT_DOUBLECLICKED:
					CowTipEvent(npc, event);
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		sleepms(5);
		event := wait_for_event(900000);
	endwhile

endprogram


function CowTipEvent(npc, byref event)
	PlaySoundEffect(npc, 0x7C);
	sleep(1);
	AI_Move(npc, event.source, NEMOVE_AWAY, NEMOVE_WALK, WAKEUP, 1);

	return 1;
endfunction
