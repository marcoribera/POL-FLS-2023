/* $Id: genericAnimal.src 1110 2006-01-09 04:17:47Z austin $
 *
 */
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:commonFunctions";
include ":attributes:attributes";
include ":brainAI:npcUtil";

program BrainNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.

	//Store it here so it does not have to do the math over and over.
	var flee_level := NPC_GetFleeLevel(npc, settings["FleeLevel"]);

	var opponent := event.source;
	var flee_mode := 0;

	AI_SetOpponent(npc, opponent);

	var my_fear_list := GetConfigStringArray(FindConfigElem(ReadConfigFile(":brainAI:config/hatelist"), NPC_ParseTemplateName(npc.npctemplate).template), "Fears");

	while ( npc )
		var dist := Distance(npc, opponent);
		if ( ShouldFlee(npc, flee_level, flee_mode) || (NPC_ParseTemplateName(opponent.npctemplate).template in my_fear_list) )
			flee_mode := 1;
			AI_Move(npc, opponent, NEMOVE_AWAY, NEMOVE_RUN, WAKEUP, 25);
		elseif ( DoneFighting(npc, opponent, dist) )
			if ( scripts.Exists("EndFight") )
				params := array{npc, "EndFight", opponent, settings, scripts};
				AI_StartNerve(npc, "EndFight", scripts["EndFight"].script, params);
			else
				AI_WarMode(npc, 0);
			endif
			AI_EndNerve(npc, nerve_name);
		elseif( dist > 1 )
			AI_Move(npc, opponent, NEMOVE_TOWARD, NEMOVE_RUN, WAKEUP, dist+1);
		endif

		sleepms(400);
		While ( Events_Waiting() )
			CheckForEvent(npc, event);
		endwhile
	endwhile
endprogram

function DoneFighting(npc, opponent, byref dist)
	if ( opponent.dead )
		return 1;
	elseif ( !opponent )
		return 1;
	elseif ( opponent.hidden )
		return 1;
	elseif ( opponent.concealed > npc.cmdlevel )
		return 1;
	elseif ( dist > 20 )
		return 1;
	elseif ( dist > 10 && !CheckLineOfSight(npc, opponent) )
		return 1;
	endif

	return 0;
endfunction

function ShouldFlee(npc, byref flee_level, byref flee_mode)
	if ( AP_GetVital(npc, "Hits") < flee_level )
		return 1;
	endif

	return 0;
endfunction


// Need more stuff here, should have events checked for peacemaking
// and provocation.
function CheckForEvent(npc, byref event)
	// Temp added for stopping ecompile spam
	npc := npc;
	event := wait_for_event(0);
	if ( event )
		var source := event.source;
		case ( event.type )
			SYSEVENT_ENGAGED: source := source; // Added temp for stopping ecompile spam
				break;
			SYSEVENT_DAMAGED:
				break;
			default:
				break;
		endcase
		event := 0;
	endif

	return 1;
endfunction
