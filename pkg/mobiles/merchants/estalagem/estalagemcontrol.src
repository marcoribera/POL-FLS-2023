use uo;
use os;
use util;

include ":datafile:datafile";
include ":containers:storageAreas";
include ":containers:containers";
include "include/say";
include ":keys:key";
include "include/client";
include ":itemutils:itemdesc";

CONST CYCLE_WAIT := 60 * 180;
const MOEDAS			:= 47715;
const MOEDAS_COBRE		:= 0xba63; //id das moedas de cobre
const MOEDAS_PRATA		:= 0xba64; //id das moedas de prata
const MOEDAS_OURO		:= 0xba65; //id das moedas de ouro
const GOLD_COIN			:= 0xeed;  //id das moedas de ouro (outro tipo válido)


program GlobalInnControl()
	//set_priority(1);
	while(1)
		InnKeeperChecks();
		sleep(CYCLE_WAIT);
	endwhile
endprogram

function InnKeeperChecks()
	var innkeeper := getglobalproperty("InnKeeper");

	if (innkeeper[1] == error)
		return 1;
	endif

	var x := 1;
	while (x <= innkeeper.size())
		var npc := SystemFindObjectBySerial(cint(innkeeper[x]));
		if (npc.errortext)
			innkeeper.erase(x);
			SetGlobalProperty("InnKeeper", innkeeper);
		endif
		var quartos := GetObjProperty(npc, "quartos");
		var mural := GetObjProperty(npc, "mural");

		if (!mural && !SystemFindObjectBySerial(cint(mural)) && !GetObjProperty(npc, "noglobalmsg"))
			var players := EnumerateOnlineCharacters();
			foreach player in players
				if (player.cmdlevel > 2 )
					SendSysMessageEx(player, "Bulletin Board: "+npc.name+" em '"+npc.x+" "+npc.y+" "+npc.z+"' nao possui Mural!", SSM_FAIL);
				endif
			endforeach
		endif

		var i := 1;
		foreach quarto in quartos
			if (quartos[i].inquilino && quartos[i].inquilino != error)
				var rentdate := quartos[i].data;

				if (rentdate == error)
					quartos[i].data := polcore().systime;
					SetObjProperty(npc, "quartos", quartos);
				endif

				var ticks := CalcularDias(rentdate);
				var renttime := GetObjProperty(npc, "tempoalugado");
				if (!renttime || renttime.errortext)
					renttime := 7;
				endif

				if (ticks >= renttime)
					var bank_region := GetObjProperty(npc, "City");
					if ( !bank_region )
						bank_region := CP_DEFAULT_AREA;
					endif
					var player2 := SystemFindObjectBySerial(quartos[i].inquilino, SYSFIND_SEARCH_OFFLINE_MOBILES);

					if ( !debitoEmConta(bank_region, player2, quartos[i].custo) )
						var door := SystemFindObjectBySerial(quarto.door);
						Eraseobjproperty(door, "LockId");
						quartos[i].inquilino := 0;
						SetObjProperty(npc, "quartos", quartos);
						if (mural && player2 != error)
							mural := SystemFindObjectBySerial(cint(mural));
							if (mural)
								BulletinReport(player2, mural, npc);
							endif
						endif
						var cent := quartos[i].centro;
						if (cent[1])
							DestroyRentItens(cent[1], cent[2], cent[3], npc.realm, player2, npc);
							ChecarMobileQuarto(player2, cent[1], cent[2], cent[3], npc);
							if (cent[4])
								DestroyRentItens(cent[4], cent[5], cent[6], npc.realm, player2, npc);
								ChecarMobileQuarto(player2, cent[4], cent[5], cent[6], npc);
							endif
						endif
					else
						quartos[i].data := polcore().systime;
						SetObjProperty(npc, "quartos", quartos);
					endif
				endif
			endif
			i := i +1;
			sleepms(20);
		endforeach
		x := x+1;
		sleepms(20);
	endwhile
	return 1;
endfunction

function DestroyRentItens(x, y, z, r, player, npc)
	var objects := ListItemsNearLocation( x, y, z, 10, r);
	var backpack;
	if (player && player != error)
		backpack := CreateItemAtLocation( x, y, z, hex(3701), 1, r);
		backpack.desc := player.name;
	endif
	foreach obj in objects
		if (CheckLosBetween( x, y, z, obj.x, obj.y, obj.z, r))
			if (GetObjProperty(obj, "crafterserial"))
				if (player && player != error)
					obj.movable := 1;
					MoveItemToContainer( obj, backpack);
				else
					DestroyItem(obj);
				endif


			elseif (obj.IsA(POLCLASS_CONTAINER))
				var container := EnumerateItemsInContainer( obj, ENUMERATE_IGNORE_LOCKED );
				foreach item in container
				if (player && player != error)
					item.movable := 1;
					MoveItemToContainer( item, backpack);
				else
					DestroyItem(item);
				endif
					sleepms(20);
				endforeach
			endif
		endif
		sleepms(20);
	endforeach
	if (player && player != error)
		if ( !npc.backpack )
			Set_Critical(1);
			var newpack := CreateItemAtLocation(npc.x, npc.y, npc.z, "backpack", 1, npc.realm);
			if ( !EquipItem(npc, newpack) )
				DestroyItem(newpack);
				return 0;
			endif
		endif
		MoveItemToContainer( backpack, npc.backpack);
		var confiscado := GetObjProperty(npc, "confiscados");
		if (!confiscado || confiscado == error)
			confiscado := struct;
		endif
		confiscado.insert(""+player.serial, backpack.serial);
		SetObjProperty(npc, "confiscados", confiscado);
	endif
	return 1;
endfunction

function debitoEmConta(bank_region, player, amount)

	var bank_box := CP_GetStorageContainerForMobile(player, bank_region, CP_NOCREATE);
	if (!bank_box)
		SendSysMEssage(player, "Voce nao tem banco");
		return 0;
	endif
	//Verifica o valos disnonível no banco
	Set_Critical(1);
	var recursosDisponiveis := CalculaRecursosDisponiveisBank(bank_box, MOEDAS_COBRE);
	SendSysMessage(player, "Valor inicial disponivel no banco: " + recursosDisponiveis[5]);
	if (recursosDisponiveis[5] < amount)
		SendSysMessageEx(player, "Falta dinheiro no banco para pagar o aluguel.");
		Set_Critical(0);
		return 0;
	else
		var totalPago := 0;
		var faltaPagar := amount;
		var dicionarioAtual := Dictionary;
		var recursos := recursosDisponiveis[5];
		var diferenca;
		var moeda;
		if (recursos >= faltaPagar)
			if (faltaPagar > 0) //Tenta pagar com os cobres
				dicionarioAtual := recursosDisponiveis[1];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					diferenca := faltaPagar - moeda.amount;
					if (diferenca > 0)
						totalPago += moeda.amount;
						faltaPagar -= moeda.amount;
						DestroyItem(moeda);
					else
						totalPago += faltaPagar;
						SubtractAmount(moeda, faltaPagar);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel");
						break;
					endif
				endforeach
			endif
			if (faltaPagar > 0) //Tenta pagar com as pratas
				dicionarioAtual := recursosDisponiveis[2];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					diferenca := faltaPagar - moeda.amount*10;
					if (diferenca > 0)
						totalPago += moeda.amount*10;
						faltaPagar -= moeda.amount*10;
						DestroyItem(moeda);
					else
						var troco := moeda.amount*10 - faltaPagar;
						DestroyItem(moeda);
						totalPago += faltaPagar;
						DarTrocoBank(player, bank_box, troco);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel.");
						break;
					endif
				endforeach
			endif
			if (faltaPagar > 0) //Tenta pagar com os ouros
				dicionarioAtual := recursosDisponiveis[3];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					//print("Serial: "+ _serial_iter +" Quantidade: "+ serial);
					//print("Moeda: "+ moeda.amount +" ");
					//print("Falta Pagar: "+ faltaPagar +" ");
					diferenca := faltaPagar - moeda.amount*100;
					if (diferenca > 0)
						totalPago += moeda.amount*100;
						faltaPagar -= moeda.amount*100;
						DestroyItem(moeda);
					else
						//print("Vai pagar com ouro");
						var troco := moeda.amount*100 - faltaPagar;
						DestroyItem(moeda);
						totalPago += faltaPagar;
						DarTrocoBank(player, bank_box, troco);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel!");
						break;
					endif
				endforeach
			endif
		else
			SendSysMessage(player, "Voce nao tem moedas suficientes no banco para pagar o aluguel.");
			Set_Critical(0);
			return 0;
		endif
	endif
	recursosDisponiveis := CalculaRecursosDisponiveisBank(bank_box, MOEDAS_COBRE);
	SendSysMessage(player, "Valor final disponivel no banco: " + recursosDisponiveis[5]);
	Set_Critical(0);
	return 1;
endfunction

function CalculaRecursosDisponiveisBank(bank_box, cost_type)
	var recursosDisponiveis := Dictionary;
	recursosDisponiveis[1] := Dictionary; //Cobre
	recursosDisponiveis[2] := Dictionary; //Prata
	recursosDisponiveis[3] := Dictionary; //Ouro
	recursosDisponiveis[4] := Dictionary; //Outros
	recursosDisponiveis[5] := 0; //Total

	foreach item in EnumerateItemsInContainer(bank_box)
		if (item.objtype == MOEDAS_COBRE)
			recursosDisponiveis[1][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount;
		elseif (item.objtype == MOEDAS_PRATA)
			recursosDisponiveis[2][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount*10;
		elseif (item.objtype == MOEDAS_OURO || item.objtype == GOLD_COIN)
			recursosDisponiveis[3][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount*100;
		endif
	endforeach
	return recursosDisponiveis;
endfunction


function DarTrocoBank(player, bank_box, ValorTroco)
	var faltaPagar := ValorTroco;
	/*
	if(ValorTroco > 0)
		SendSysMessage(player, "Restam no banco:"); //Comentar quando for colocar em produção
	endif
	*/
	var ouro := CInt(faltaPagar/100);
	if(ouro > 0)
		CreateItemInContainer(bank_box, MOEDAS_OURO, ouro);
		//SendSysMessage(player, ouro + " moedas de ouro");
	endif
	faltaPagar -= CInt(ouro*100);

	var prata := CInt(faltaPagar/10);
	if(prata > 0)
		CreateItemInContainer(bank_box, MOEDAS_PRATA, prata);
		//SendSysMessage(player, prata + " moedas de prata");
	endif
	faltaPagar -= CInt(prata*10);

	var cobre := CInt(faltaPagar);
	if(cobre > 0)
		CreateItemInContainer(bank_box, MOEDAS_COBRE, cobre);
		//SendSysMessage(player, cobre + " moedas de cobre");
	endif
	return 1;
endfunction

function CheckGoldAmount(bank_box, amount)

	var prop := dictionary;
	prop["objtype"] := 47715;
	var total_gold := AmountInContainer(bank_box, prop );
	if ( amount == 0 )
		return 0;
	elseif ( amount > total_gold )
		return 0;
	else
		return 1;
	endif

endfunction
function ChecarMobileQuarto(who, x, y, z, npc)
	if ( CheckLosAt( who, x, y, z ) )
		if (!MoveObjectToLocation(who, npc.x, npc.y+3, npc.z, npc.realm))
			if (!MoveObjectToLocation(who, npc.x, npc.y-3, npc.z, npc.realm))
				if (!MoveObjectToLocation(who, npc.x+3, npc.y, npc.z, npc.realm))
					MoveObjectToLocation(who, npc.x-3, npc.y, npc.z, npc.realm);
				endif
			endif
		endif
	endif
endfunction


function BulletinReport(who, board, npc)
	var board_data_file := DFOpenDataFile(":bulletinboard:MessageBoards", DF_CREATE);
	var board_elem := DFFindElement(board_data_file, hex(board.serial), DF_CREATE);
	var msg_string := "O pagamento da sua estalagem nao foi efetuado como esperado. Estarei colocando a estalagem de novo a disposicao de novos moradores, e armazenarei seus objetos ate que pague uma pequena taxa. Me procure.";
	var post_num := CInt(board_elem.GetProp("NextPost"));
	var new_num := CInt(board_elem.GetProp("NextPost"))+1;
	if (new_num > 50)
		//Were on post 50, reset the counter.
		new_num := 1;
	endif
	board_elem.SetProp("NextPost", new_num);

	var post_info := struct;
	post_info.+Name := " " + npc.name;
	post_info.+Subject := "Aviso de Despejo: " +who.name;
	post_info.+Message := msg_string;
	post_info.+Data := polcore().systime;
	post_info.+Serial := who.serial;
	post_info.+GMPage := 0;

	return board_elem.SetProp(hex(post_num), post_info);
endfunction

function CalcularDias(data)
    var criadoEm := cint(data);
    var tempoAgora := polcore().systime;// segundos
    var tempoDevida:= tempoAgora - criadoEm;
    var dias := cint(tempoDevida/(86400));//24horas*60minutos*60segundos = 86400 segundos
    return dias;
endfunction